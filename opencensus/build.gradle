plugins {
    id 'java-library'
    id 'maven-publish'
}

apply from: "../versions.gradle"

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

dependencies {
    api "com.google.protobuf:protobuf-java:${versions.protobufJava}"
    api "io.grpc:grpc-protobuf:${versions.grpcProtobuf}"
}

def signingKey = findProperty("signingKey")
def signingPassword = findProperty("signingPassword")
if (signingKey != null) {
    apply plugin: 'signing'
    signing {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

// there is an issue with the proto compiled javadoc where it breaks doclint so, this disables
// doclint from running as per the info at :
// https://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                from components.java

                version traceVersions.opencensus

                pom {
                    name = 'Trace SDK internal OpenCensus module'
                    packaging 'jar'
                    description = 'Module containing the OpenCensus compiled proto files, for use inside the Trace SDK.'
                    url = 'https://github.com/bitrise-io/trace-android-sdk'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/bitrise-io/trace-android-sdk/license.md'
                        }
                    }
                    developers {
                        developer {
                            id = 'trace'
                            name = 'Trace Team'
                            email = 'trace-bot@bitrise.io'
                        }
                    }
                    scm {
                        connection = 'https://github.com/bitrise-io/trace-android-sdk.git'
                        developerConnection = 'git@github.com:bitrise-io/trace-android-sdk.git'
                        url = 'https://github.com/bitrise-io/trace-android-sdk'
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                    def ossrhUsername = findProperty("sonatypeUsername")
                    def ossrhPassword = findProperty("sonatypePassword")
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }
}